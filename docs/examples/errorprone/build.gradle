///////////////////////////////////////////////////////////////////////////
/// Checker Framework pluggable type-checking and Error Prone example
///

plugins {
    id 'java'
    id 'net.ltgt.errorprone' version '4.0.1'
    // Checker Framework pluggable type-checking
    id 'org.checkerframework' version '0.6.45'
}

ext {
    versions = [
        eisopVersion: '3.42.0-eisop4',
    ]
}

apply plugin: 'org.checkerframework'
dependencies {
    compileOnly "io.github.eisop:checker-qual:${versions.eisopVersion}"
    testCompileOnly "io.github.eisop:checker-qual:${versions.eisopVersion}"
    checkerFramework "io.github.eisop:checker:${versions.eisopVersion}"
}

dependencies {
    // Must use at least version 2.4.0 of Error Prone.
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        errorprone 'com.google.errorprone:error_prone_core:2.10.0'
    } else if (JavaVersion.current() < JavaVersion.VERSION_17) {
        errorprone 'com.google.errorprone:error_prone_core:2.31.0'
    } else {
        errorprone 'com.google.errorprone:error_prone_core:2.33.0'
    }
}

repositories {
    mavenCentral()
}

checkerFramework {
    checkers = [
        'org.checkerframework.checker.nullness.NullnessChecker',
    ]
}

compileJava {
    // A checker will only run if Error Prone does not issue any warnings.  So
    // convert the expected error to a warning to test that both Error Prone
    // and the Nullness Checker run.
    options.errorprone.warn('CollectionIncompatibleType')
}
